buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "de.baerinteractive"
archivesBaseName = "pixlhash"
version = "0.1.0"

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 23
        versionCode 1
        versionName "0.1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'PixlHash'
                packaging 'aar'
                artifactId 'pixlhash'
                description 'This library is used to convert a String into a visual representation.' +
                        'This representation is returned as a Bitmap.' +
                        'It can be used as an Avatar or other form of identification.'
                url 'https://github.com/Fettn/Android-PixlHash'

                scm {
                    connection 'scm:git:https://github.com/Fettn/Android-PixlHash.git'
                    developerConnection 'scm:git:https://github.com/Fettn/Android-PixlHash.git'
                    url 'https://github.com/Fettn/Android-PixlHash'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'fettn'
                        name 'Fettn'
                        email 'fettn@baerinteractive.de'
                    }
                }
            }
        }
    }
}